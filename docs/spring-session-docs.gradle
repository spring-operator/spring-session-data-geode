apply plugin: 'io.spring.convention.docs'
apply plugin: 'io.spring.convention.spring-test'
apply from: IDE_GRADLE

dependencies {

	testCompile project(':spring-session-data-geode')
	testCompile "javax.servlet:javax.servlet-api"
	testCompile "junit:junit"
	testCompile "org.assertj:assertj-core"
	testCompile "org.mockito:mockito-core"
	testCompile "edu.umd.cs.mtc:multithreadedtc"
	testCompile "org.springframework:spring-test"
	testCompile "org.springframework.security:spring-security-core"
	testCompile "org.springframework.security:spring-security-test"
	testCompile "org.springframework.security:spring-security-web"

}

def versions = dependencyManagement.managedVersions

asciidoctor {

	def ghTag = snapshotBuild ? 'master' : project.version
	def ghUrl = "https://github.com/spring-projects/spring-session-data-geode/tree/$ghTag"

	attributes 'version-snapshot': snapshotBuild,
		'version-milestone': milestoneBuild,
		'version-release': releaseBuild,
		'gh-url': ghUrl,
		'gh-samples-url': "$ghUrl/samples/",
		'download-url' : "https://github.com/spring-projects/spring-session-data-geode/archive/${ghTag}.zip",
//		'spring-version' : "$springVersion",
		'spring-version' : versions['org.springframework:spring-core'],
		'spring-session-version' : version,
		'docs-itest-dir' : rootProject.projectDir.path + '/docs/src/integration-test/java/',
		'docs-test-dir' : rootProject.projectDir.path + '/docs/src/test/java/',
		'docs-test-resources-dir' : rootProject.projectDir.path + '/docs/src/test/resources/',
		'samples-dir' : rootProject.projectDir.path + '/samples/',
		'session-main-resources-dir' : rootProject.projectDir.path + '/spring-session/src/main/resources/'
}

javadoc {
	configure(options) {
		links = [
			"https://docs.spring.io/spring/docs/current/javadoc-api/",
			"https://docs.spring.io/spring-data/commons/docs/current/api/index.html?index-all.html",
			"https://docs.spring.io/spring-data-geode/docs/current/api/",
			"https://docs.spring.io/spring-session/docs/${springSessionVersion}/api/",
			"https://geode.apache.org/releases/latest/javadoc/index.html",
		]
	}
}
